10 'STUDENT INFORMATION MANAGEMENT SOFTWARE
20 CLS: SCREEN 0, 1: WIDTH 80 'COLOR TEXT MODE
30 LOCATE 5, 20: COLOR 9: PRINT "STUDENT INFORMATION  MANAGEMENT SOFTWARE"
40 LOCATE 10, 39: COLOR 6: PRINT "By"
50 LOCATE 14, 36: COLOR 2: PRINT "ASMIT DE"
60 LOCATE 15, 32: COLOR 2: PRINT "DEEPTIMAN  DATTA"
70 LOCATE 16, 34: COLOR 2: PRINT "DEBARPAN DAN"
80 LOCATE 17, 33: COLOR 2: PRINT "MANISHA  GUPTA"
90 LOCATE 18, 33: COLOR 2: PRINT "ARIJIT GANGULY"
100 LOCATE 20, 30: COLOR 12: PRINT "CLASS X   SEC D(AFT)"
110 LOCATE 24, 1: COLOR 15: PRINT "PRESS ANY KEY TO CONTINUE ....."
120 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 120


130 'MAIN MENU
140 DIM ASID(999), ASFN$(999), ASSN$(999), ASCL(999), ASSEC$(999), ASRN(999)
150 DIM AMID(100), AMFL(100), AMSL(100), AMMAT(100), AMPSC(100), AMLSC(100), AMHST(100), AMGEO(100)
160 CLS: SCREEN 0,1: LOCATE 2,38: COLOR 9: PRINT "MENU": PRINT
170 COLOR 10: PRINT TAB(5); "KEY"; TAB(44); "FUNCTION": PRINT
180 COLOR 15: PRINT TAB(6); "1"; TAB(44); "SEARCH STUDENT RECORD": PRINT
190 COLOR 15: PRINT TAB(6); "2"; TAB(44); "CREATE STUDENT RECORD": PRINT
200 COLOR 15: PRINT TAB(6); "3"; TAB(44); "EDIT STUDENT RECORD": PRINT
210 COLOR 15: PRINT TAB(6); "4"; TAB(44); "DELETE STUDENT RECORD": PRINT
220 COLOR 15: PRINT TAB(6); "5"; TAB(44); "DISPLAY MARKSHEET": PRINT
230 COLOR 15: PRINT TAB(6); "6"; TAB(44); "CREATE MARKSHEET": PRINT
240 COLOR 15: PRINT TAB(6); "7"; TAB(44); "EDIT MARKSHEET": PRINT
250 COLOR 15: PRINT TAB(6); "8"; TAB(44); "DELETE MARKSHEET": PRINT
260 COLOR 15: PRINT TAB(6); "9"; TAB(44); "TO EXIT"
270 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 270
280 IF CHK$ = "1" THEN GOSUB 590: GOTO 370
290 IF CHK$ = "2" THEN GOSUB 1170: GOTO 370
300 IF CHK$ = "3" THEN GOSUB 1410: GOTO 370
310 IF CHK$ = "4" THEN GOSUB 2250: GOTO 370
320 IF CHK$ = "5" THEN GOSUB 2550: GOTO 370
330 IF CHK$ = "6" THEN GOSUB 3020: GOTO 370
340 IF CHK$ = "7" THEN GOSUB 3340: GOTO 370
350 IF CHK$ = "8" THEN GOSUB 4120: GOTO 370
360 IF CHK$ = "9" THEN GOTO 400 ELSE BEEP: GOTO 270
370 ERASE ASID, ASFN$, ASSN$, ASCL, ASSEC$, ASRN
380 ERASE AMID, AMFL, AMSL, AMMAT, AMPSC, AMLSC, AMHST, AMGEO
390 GOTO 130
400 SYSTEM


410 'SUBROUTINE FOR STUDENT INFORMATION ARRAY LOAD
420 OPEN "STUDENT.TXT" FOR INPUT AS #1: K=0
430 WHILE NOT EOF(1)
440 K=K+1
450 INPUT #1, ASID(K), ASFN$(K), ASSN$(K), ASCL(K), ASSEC$(K), ASRN(K)
460 WEND
470 CLOSE #1
480 RETURN


490 'SUBROUTINE FOR LETTER ERROR CHECK
500 FLAG=0
510 L = LEN(TMP$)
520 FOR I = 1 TO L
530 E$=MID$(TMP$,I,1): AE=ASC(E$)
540 IF AE>=65 AND AE<=90 THEN GOTO 570
550 IF AE>=97 AND AE<=122 THEN GOTO 570
560 FLAG=1
570 NEXT I
580 RETURN


590 'SUBROUTINE FOR SEARCH STUDENT RECORD
600 CLS
610 PRINT "TO SEARCH BY ID .....PRESS S"
620 PRINT "TO DISPLAY STUDENT INFORMATION LIST .....PRESS L"
630 PRINT "TO RETURN TO MENU .....PRESS R"
640 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 640
650 IF CHK$ = "S" OR CHK$ = "s" THEN GOSUB 690: GOTO 680
660 IF CHK$ = "L" OR CHK$ = "l" THEN GOSUB 900: GOTO 680
670 IF CHK$ = "R" OR CHK$ = "r" THEN GOTO 680 ELSE BEEP: GOTO 640
680 RETURN


690 'SUB-SUBROUTINE FOR SEARCH STUDENT INFORMATION BY ID
700 PRINT: INPUT "ENTER ID";SID
710 OPEN "STUDENT.TXT" FOR INPUT AS #2
720 WHILE NOT EOF(2)
730 INPUT #2, ID, F$, S$, CL, SEC$, RN
740 IF ID=SID THEN CLOSE #2: GOTO 780
750 WEND
760 CLOSE #2
770 PRINT "RECORD NOT FOUND": GOTO 850
780 CLS: COLOR 9: PRINT "------------------------------STUDENT  INFORMATION------------------------------": PRINT
790 COLOR 15: PRINT "ID      :";ID
800 COLOR 15: PRINT "NAME    : ";F$+" "+S$
810 COLOR 15: PRINT "CLASS   :";CL
820 COLOR 15: PRINT "SECTION : ";SEC$
830 COLOR 15: PRINT "ROLL NO :";RN
840 PRINT: PRINT: PRINT
850 PRINT "DO YOU WANT TO SEARCH BY ID AGAIN? (Y/N)"
860 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 860
870 IF CHK$ = "Y" OR CHK$="y" THEN GOTO 690
880 IF CHK$ = "N" OR CHK$ = "n" THEN GOTO 890 ELSE BEEP: GOTO 860
890 RETURN


900 'SUB-SUBROUTINE FOR DISPLAY STUDENT INFORMATION LIST
910 FLAGA=0: PRINT: INPUT "ENTER CLASS";SCL: IF SCL>=0 AND SCL<=12 THEN GOTO 920 ELSE BEEP: PRINT "INVALID DATA": GOTO 910
920 INPUT "ENTER SECTION (TYPE 'ALL' FOR ALL SECTIONS)";SSEC$
930 IF SSEC$="ALL" OR SSEC$="all" OR SSEC$="ALl" OR SSEC$="AlL" OR SSEC$="All" OR SSEC$="aLL" OR SSEC$="aLl" OR SSEC$="alL" THEN FLAGA=1: GOTO 960
940 IF LEN(SSEC$)>=2 THEN BEEP: PRINT "INVALID DATA": GOTO 920
950 TMP$=SSEC$: GOSUB 490
960 CLS: K=0: OPEN "STUDENT.TXT" FOR INPUT AS #3
970 WHILE NOT EOF(3)
980 IF K=0 THEN CLS: COLOR 9: PRINT "ID"; TAB(11); "NAME"; TAB(55); "CLASS"; TAB(65); "SECTION"; TAB(75); "ROLL": PRINT: COLOR 15
990 K=K+1
1000 INPUT #3, ID, F$, S$, CL, SEC$, RN
1020 IF CL<>SCL THEN GOTO 1130
1030 IF FLAGA=1 THEN GOTO 1050
1040 IF SEC$<>SSEC$ THEN GOTO 1130
1050 COLOR 15: PRINT ID; TAB(11); F$+" "+S$; TAB(55); CL; TAB(65); SEC$; TAB(75); RN
1060 IF K=18 THEN GOTO 1070 ELSE GOTO 1130
1070 PRINT: PRINT "PRESS C TO CONTINUE"
1080 PRINT "PRESS R TO RETURN TO MENU"
1090 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 1090
1100 IF CHK$ = "C" OR CHK$ = "c" THEN GOTO 1120
1110 IF CHK$ = "R" OR CHK$ = "r" THEN CLOSE #3: GOTO 1160 ELSE BEEP: GOTO 1090
1120 K=0
1130 WEND
1140 CLOSE #3: PRINT: PRINT "PRESS ANY KEY TO RETURN TO MENU"
1150 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 1150
1160 RETURN


1170 'SUBROUTINE FOR CREATE STUDENT RECORD
1180 GOSUB 410: CLS: INPUT "ENTER ID";CID: GOSUB 4600
1190 IF FLAGCID=1 THEN GOTO 1350 ELSE FLAG=0: OPEN "STUDENT.TXT" FOR OUTPUT AS #2
1200 FOR I = 1 TO K
1210 WRITE #2, ASID(I), ASFN$(I), ASSN$(I), ASCL(I), ASSEC$(I), ASRN(I)
1220 NEXT I
1230 CLS: COLOR 9: PRINT "------------------------------STUDENT  INFORMATION------------------------------": PRINT: COLOR 15
1240 PRINT "ID: ";CID
1250 INPUT "ENTER FIRST NAME";F$: TMP$=F$: GOSUB 490
1260 IF FLAG=1 THEN BEEP: PRINT "INVALID DATA": GOTO 1250
1270 INPUT "ENTER SURNAME";S$: TMP$=S$: GOSUB 490
1280 IF FLAG=1 THEN BEEP: PRINT "INVALID DATA": GOTO 1270
1290 INPUT "ENTER CLASS";CL: IF CL>=1 AND CL<=12 THEN GOTO 1300 ELSE BEEP: PRINT "INVALID DATA": GOTO 1290
1300 INPUT "ENTER SECTION";SEC$: TMP$=SEC$: GOSUB 490
1310 IF FLAG=1 OR L>1 THEN BEEP: PRINT "INVALID DATA": GOTO 1300
1320 INPUT "ENTER ROLL NO";RN :IF RN<=0 THEN BEEP: PRINT "INVALID DATA": GOTO 1320
1330 WRITE #2, CID, F$, S$, CL, SEC$, RN
1340 PRINT "---------------------------RECORD ADDED SUCCESSFULLY----------------------------"
1350 PRINT "DO YOU WANT TO ADD ANOTHER RECORD? (Y/N)"
1360 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 1360
1370 IF CHK$ = "Y" OR CHK$ = "y" THEN GOTO 1230
1380 IF CHK$ = "N" OR CHK$ = "n" THEN GOTO 1390 ELSE BEEP: GOTO 1360
1390 CLOSE #2
1400 RETURN


1410 'SUBROUTINE FOR EDIT STUDENT RECORD
1420 CLS
1430 PRINT "TO EDIT BY ID .....PRESS I"
1440 PRINT "TO PERFORM GLOBAL EDIT OF CLASS .....PRESS G"
1450 PRINT "TO RETURN TO MENU .....PRESS R"
1460 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 1460
1470 IF CHK$ = "I" OR CHK$ = "i" THEN GOSUB 1510: GOTO 1500
1480 IF CHK$ = "G" OR CHK$ = "g" THEN GOSUB 2070: GOTO 1500
1490 IF CHK$ = "R" OR CHK$ = "r" THEN GOTO 1500 ELSE BEEP: GOTO 1460
1500 RETURN


1510 'SUB-SUBROUTINE FOR EDIT STUDENT RECORD BY ID
1520 GOSUB 410
1530 PRINT: INPUT "ENTER ID";ID
1540 FLAGF=0: FLAGC=0
1550 OPEN "STUDENT.TXT" FOR OUTPUT AS #2
1560 FOR I = 1 TO K
1570 IF ASID(I)=ID THEN GOTO 1590
1580 WRITE #2, ASID(I), ASFN$(I), ASSN$(I), ASCL(I), ASSEC$(I), ASRN(I): GOTO 1600
1590 FLAGF=1: CID=ASID(I): CF$=ASFN$(I): CS$=ASSN$(I): CCL=ASCL(I): CSEC$=ASSEC$(I): CRN=ASRN(I): PI=I
1600 NEXT I
1610 CLS: COLOR 9: PRINT "------------------------------STUDENT  INFORMATION------------------------------": PRINT: COLOR 15
1620 PRINT "ID      :";ID
1630 PRINT "NAME    : ";CF$+" "+CS$
1640 PRINT "CLASS   :";CCL
1650 PRINT "SECTION : ";CSEC$
1660 PRINT "ROLL NO :";CRN
1670 PRINT: PRINT
1680 PRINT "TO EDIT NAME .....PRESS N"
1690 PRINT "TO EDIT CLASS .....PRESS C"
1700 PRINT "TO EDIT SECTION .....PRESS S"
1710 PRINT "TO EDIT ROLL NO .....PRESS R"
1720 PRINT "TO APPLY THE CHANGES .....PRESS A"
1730 PRINT "TO DISCARD THE CHANGES .....PRESS D"
1740 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 1740
1750 IF CHK$ = "N" OR CHK$ = "n" THEN GOSUB 1820: GOTO 1810
1760 IF CHK$ = "C" OR CHK$ = "c" THEN GOSUB 1880: GOTO 1810
1770 IF CHK$ = "S" OR CHK$ = "s" THEN GOSUB 1910: GOTO 1810
1780 IF CHK$ = "R" OR CHK$ = "r" THEN GOSUB 1950: GOTO 1810
1790 IF CHK$ = "A" OR CHK$ = "a" THEN GOTO 1800
1795 IF CHK$ = "D" OR CHK$ = "d" THEN WRITE #2, ASID(PI), ASFN$(PI), ASSN$(PI), ASCL(PI), ASSEC$(PI), ASRN(PI): GOTO 1980 ELSE BEEP: GOTO 1740
1800 FLAGC=1: WRITE #2, ID, CF$, CS$, CCL, CSEC$, CRN: GOTO 1980 
1810 GOTO 1610
1820 CLS: PRINT "NAME : ";CF$+" "+CS$
1830 INPUT "ENTER NEW FIRST NAME";CF$: TMP$=CF$: GOSUB 490
1840 IF FLAG=1 THEN BEEP: PRINT "INVALID DATA": GOTO 1830
1850 PRINT: INPUT "ENTER NEW SURNAME";CS$: TMP$=CS$: GOSUB 490
1860 IF FLAG=1 THEN BEEP: PRINT "INVALID DATA": GOTO 1850
1870 RETURN
1880 CLS: PRINT "CLASS :";CCL
1890 INPUT "ENTER NEW CLASS";CCL: IF CCL>=1 AND CCL<=12 THEN GOTO 1900 ELSE BEEP: PRINT "INVALID DATA": GOTO 1890
1900 RETURN
1910 CLS: PRINT "SECTION : ";CSEC$
1920 INPUT "ENTER NEW SECTION";CSEC$: TMP$=CSEC$: GOSUB 490
1930 IF FLAG=1 OR L>1 THEN BEEP: PRINT "INVALID DATA": GOTO 1920
1940 RETURN
1950 CLS: PRINT "ROLL NO :";CRN
1960 INPUT "ENTER ROLL NO";CRN :IF CRN<=0 THEN BEEP: PRINT "INVALID DATA": GOTO 1960
1970 RETURN 
1980 IF FLAGF=0 THEN  BEEP: PRINT "RECORD NOT FOUND": GOTO 2000
1990 IF FLAGC=1 THEN PRINT "--------------------------RECORD CHANGED SUCCESSFULLY---------------------------"
2000 CLOSE #2
2010 PRINT: PRINT "DO YOU WANT TO EDIT ANOTHER RECORD? (Y/N)"
2020 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 2020
2030 IF CHK$ = "Y" OR CHK$ = "y" THEN GOTO 1510
2040 IF CHK$ = "N" OR CHK$ = "n" THEN GOTO 2050 ELSE BEEP: GOTO 2020
2050 RETURN


2070 'SUB-SUBROUTINE FOR GLOBAL EDIT OF CLASS
2080 GOSUB 410: FLAGF=0
2090 PRINT: INPUT "ENTER THE CLASS TO EDIT";GCL
2100 INPUT "ENTER NEW CLASS";NCL
2110 OPEN "STUDENT.TXT" FOR OUTPUT AS #3
2120 FOR I = 1 TO K
2130 IF ASCL(I)=GCL THEN GOTO 2150
2140 WRITE #3, ASID(I), ASFN$(I), ASSN$(I), ASCL(I), ASSEC$(I), ASRN(I): GOTO 2160
2150 FLAGF=1: WRITE #3, ASID(I), ASFN$(I), ASSN$(I), NCL, ASSEC$(I), ASRN(I)
2160 NEXT I
2170 IF FLAGF=0 THEN PRINT "CLASS NOT FOUND": GOTO 2190
2180 PRINT: PRINT "---------------------------CLASS CHANGED SUCCESSFULLY---------------------------"
2190 CLOSE #3
2200 PRINT: PRINT "DO YOU WANT TO EDIT ANOTHER CLASS? (Y/N)"
2210 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 2210
2220 IF CHK$ = "Y" OR CHK$ = "y" THEN GOTO 2070 
2230 IF CHK$ = "N" OR CHK$ = "n" THEN GOTO 2240 ELSE BEEP: GOTO 2210
2240 RETURN


2250 'SUBROUTINE FOR DELETE STUDENT RECORD
2260 GOSUB 410
2270 CLS
2280 INPUT "ENTER ID";ID
2290 FLAGF=0: FLAGD=0
2300 OPEN "STUDENT.TXT" FOR OUTPUT AS #2
2310 FOR I = 1 TO K
2320 IF ASID(I)<>ID THEN GOTO 2450
2330 FLAGF=1
2340 CLS: COLOR 9: PRINT "------------------------------STUDENT  INFORMATION------------------------------": PRINT: COLOR 15
2350 PRINT "ID      :";ASID(I)
2360 PRINT "NAME    : ";ASFN$(I)+" "+ASSN$(I)
2370 PRINT "CLASS   :";ASCL(I)
2380 PRINT "SECTION : ";ASSEC$(I)
2390 PRINT "ROLL NO :";ASRN(I)
2400 PRINT: PRINT
2410 PRINT "ARE YOU SURE YOU WANT TO DELETE THIS RECORD? (Y/N)"
2420 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 2420
2430 IF CHK$ = "Y" OR CHK$ = "y" THEN FLAGD=1: GOTO 2460
2440 IF CHK$ = "N" OR CHK$ = "n" THEN FLAGD=0: GOTO 2450 ELSE BEEP: GOTO 2420
2450 WRITE #2, ASID(I), ASFN$(I), ASSN$(I), ASCL(I), ASSEC$(I), ASRN(I)
2460 NEXT I
2470 IF FLAGF=0 THEN  BEEP: PRINT "RECORD NOT FOUND": GOTO 2500
2480 IF FLAGD=1 THEN PRINT "--------------------------RECORD DELETED SUCCESSFULLY---------------------------"
2490 CLOSE #2
2500 PRINT: PRINT "DO YOU WANT TO DELETE ANOTHER RECORD? (Y/N)"
2510 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 2510
2520 IF CHK$ = "Y" OR CHK$ = "y" THEN GOTO 2250
2530 IF CHK$ = "N" OR CHK$ = "n" THEN GOTO 2540 ELSE BEEP: GOTO 2510
2540 RETURN


2550 'SUBROUTINE FOR DISPLAY MARKSHEET
2560 OPEN "STUDENT.TXT" FOR INPUT AS #1
2570 OPEN "MARKS.TXT" FOR INPUT AS #2
2580 CLS: FLAGFS=0: FLAGFM=0: INPUT "ENTER ID";SID
2590 WHILE NOT EOF(1)
2660 INPUT #1, ID, F$, S$, CL, SEC$, RN
2670 IF SID=ID THEN FLAGFS=1: DID=ID: DF$=F$: DS$=S$: DCL=CL: DSEC$=SEC$: DRN=RN
2680 WEND
2690 CLOSE #1
2700 IF FLAGFS=0 THEN BEEP: PRINT "RECORD NOT FOUND": GOTO 2900
2710 WHILE NOT EOF(2)
2720 INPUT #2, ID, FL, SL, MAT, PSC, LSC, HST, GEO
2730 IF SID=ID THEN FLAGFM=1: DID=ID: DFL=FL: DSL=SL: DMAT=MAT: DPSC=PSC: DLSC=LSC: DHST=HST: DGEO=GEO
2740 WEND
2750 IF FLAGFM=0 THEN BEEP: PRINT "MARKSHEET NOT FOUND": GOTO 2900
2760 CLS: COLOR 6
2770 PRINT "############################### PROGRESS REPORT ###############################"
2780 PRINT TAB(4); "NAME: "; DF$+" "+DS$; TAB(66); "ID:"; DID
2790 PRINT TAB(4); "CLASS:"; DCL; TAB(35); "SECTION: "; SEC$; TAB(66); "ROLL:"; DRN
2800 PRINT "###############################################################################"
2810 COLOR 4: PRINT TAB(10); "SUBJECT"; TAB(66); "MARKS"
2820 COLOR 14: PRINT: PRINT TAB(10); "FIRST LANGUAGE"; TAB(66); DFL
2830 COLOR 14: PRINT: PRINT TAB(10); "SECOND LANGUAGE"; TAB(66); DSL
2840 COLOR 14: PRINT: PRINT TAB(10); "MATHEMATICS"; TAB(66); DMAT
2850 COLOR 14: PRINT: PRINT TAB(10); "PHYSICAL SCIENCES"; TAB(66); DPSC
2860 COLOR 14: PRINT: PRINT TAB(10); "LIFE SCIENCE"; TAB(66); DLSC
2870 COLOR 14: PRINT: PRINT TAB(10); "HISTORY"; TAB(66); DHST
2880 COLOR 14: PRINT: PRINT TAB(10); "GEOGRAPHY"; TAB(66); DGEO
2890 COLOR 14: PRINT: PRINT TAB(10); "GRAND TOTAL:"; DFL+DSL+DMAT+DPSC+DLSC+DHST+DGEO; TAB(55); "PERCENTAGE:"; (DFL+DSL+DMAT+DPSC+DLSC+DHST+DGEO)/8; "%"
2900 COLOR 15: PRINT: PRINT "PRESS ANY KEY TO RETURN TO MENU"
2910 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 2910
2920 CLOSE #2
2930 RETURN


2940 'SUBROUTINE FOR MARKS ARRAY LOAD
2950 OPEN "MARKS.TXT" FOR INPUT AS #1: K=0
2960 WHILE NOT EOF(1)
2970 K=K+1
2980 INPUT #1, AMID(K), AMFL(K), AMSL(K), AMMAT(K), AMPSC(K), AMLSC(K), AMHST(K), AMGEO(K)
2990 WEND
3000 CLOSE #1
3010 RETURN


3020 'SUBROUTINE FOR CREATE MARKSHEET
3030 GOSUB 2940
3040 FLAGF=0: OPEN "STUDENT.TXT" FOR INPUT AS #2
3050 CLS: INPUT "ENTER ID";EID
3060 WHILE NOT EOF(2)
3070 INPUT #2, ID, F$, S$, CL, SEC$, RN
3080 IF EID=ID THEN FLAGF=1: DID=ID: DF$=F$: DS$=S$: DCL=CL: DSEC$=SEC$: DRN=RN
3090 WEND
3100 CLOSE #2
3110 IF FLAGF=0 THEN BEEP: PRINT "RECORD NOT FOUND": GOTO 3290
3120 GOSUB 4500: IF FLAGI=1 THEN GOTO 3290 ELSE OPEN "I:\IX2006\PROJECTS\SIMS\MARKS.TXT" FOR OUTPUT AS #3
3130 FOR I = 1 TO K
3140 WRITE #3, AMID(I), AMFL(I), AMSL(I), AMMAT(I), AMPSC(I), AMLSC(I), AMHST(I), AMGEO(I)
3150 NEXT I
3160 CLS: COLOR 9: PRINT "------------------------------STUDENT  INFORMATION------------------------------": PRINT: COLOR 15
3170 PRINT "ID:"; DID; "   NAME: "; DF$+" "+DS$; "    CLASS:"; DCL; "   SECTION: "; DSEC$; "    ROLL:"; DRN
3180 COLOR 9: PRINT: PRINT "-----------------------------------MARKSHEET------------------------------------": PRINT: COLOR 15
3190 INPUT "ENTER MARKS IN FIRST LANGUAGE";FL: IF FL<0 OR FL>200 THEN BEEP: PRINT "INVALID DATA": GOTO 3190
3200 INPUT "ENTER MARKS IN SECOND LANGUAGE";SL: IF SL<0 OR SL>100 THEN BEEP: PRINT "INVALID DATA": GOTO 3200
3210 INPUT "ENTER MARKS IN MATHEMATICS";MAT: IF MAT<0 OR MAT>100 THEN BEEP: PRINT "INVALID DATA": GOTO 3210
3220 INPUT "ENTER MARKS IN PHYSICAL SCIENCES";PSC: IF PSC<0 OR PSC>100 THEN BEEP: PRINT "INVALID DATA": GOTO 3220
3230 INPUT "ENTER MARKS IN LIFE SCIENCE";LSC: IF LSC<0 OR LSC>100 THEN BEEP: PRINT "INVALID DATA": GOTO 3230
3240 INPUT "ENTER MARKS IN HISTORY";HST: IF HST<0 OR HST>100 THEN BEEP: PRINT "INVALID DATA": GOTO 3240
3250 INPUT "ENTER MARKS IN GEOGRAPHY";GEO: IF GEO<0 OR GEO>100 THEN BEEP: PRINT "INVALID DATA": GOTO 3250
3260 WRITE #3, EID, FL, SL, MAT, PSC, LSC, HST, GEO
3270 PRINT "---------------------------RECORD ADDED SUCCESSFULLY----------------------------"
3280 CLOSE #3
3290 PRINT : PRINT "DO YOU WANT TO CREATE ANOTHER MARKSHEET? (Y/N)"
3300 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 3300
3310 IF CHK$ = "Y" OR CHK$ = "y" THEN GOTO 3020
3320 IF CHK$ = "N" OR CHK$ = "n" THEN GOTO 3330 ELSE BEEP: GOTO 3300
3330 RETURN


3340 'SUBROUTINE FOR EDIT MARKSHEET
3350 GOSUB 2940
3360 CLS: PRINT: INPUT "ENTER ID";SID
3370 FLAGC=0: FLAGF=0
3380 OPEN "STUDENT.TXT" FOR INPUT AS #1
3390 WHILE NOT EOF(1)
3400 INPUT #1, ID, F$, S$, CL, SEC$, RN
3410 IF SID=ID THEN FLAGF=1: DID=ID: DF$=F$: DS$=S$: DCL=CL: DSEC$=SEC$: DRN=RN
3420 WEND
3430 CLOSE #1
3440 IF FLAGF=0 THEN BEEP: PRINT "RECORD NOT FOUND": GOTO 4070
3450 OPEN "MARKS.TXT" FOR OUTPUT AS #2
3460 FOR I = 1 TO K
3470 IF AMID(I)=SID THEN GOTO 3490
3480 WRITE #2, AMID(I), AMFL(I), AMSL(I), AMMAT(I), AMPSC(I), AMLSC(I), AMHST(I), AMGEO(I): GOTO 3500
3490 CID=AMID(I): CFL=AMFL(I): CSL=AMSL(I): CMAT=AMMAT(I): CPSC=AMPSC(I): CLSC=AMLSC(I): CHST=AMHST(I): CGEO=AMGEO(I): PI=I
3500 NEXT I
3510 CLS: COLOR 9: PRINT "------------------------------STUDENT  INFORMATION-----------------------------": COLOR 15
3520 PRINT "ID:"; DID; "   NAME: "; DF$+" "+DS$; "    CLASS:"; DCL; "   SECTION: "; DSEC$; "    ROLL:"; DRN
3530 COLOR 9: PRINT "-----------------------------------MARKSHEET-----------------------------------": COLOR 15
3540 PRINT "FIRST LANGUAGE    :";CFL
3550 PRINT "SECOND LANGUAGE   :";CSL
3560 PRINT "MATHEMATICS       :";CMAT
3570 PRINT "PHYSICAL SCIENCES :";CPSC
3580 PRINT "LIFE SCIENCE      :";CLSC
3590 PRINT "HISTORY           :";CHST
3600 PRINT "GEOGRAPHY         :";CGEO
3610 PRINT: PRINT
3620 PRINT "TO EDIT MARKS IN FIRST LANGUAGE .....PRESS F"
3630 PRINT "TO EDIT MARKS IN SECOND LANGUAGE .....PRESS S"
3640 PRINT "TO EDIT MARKS IN MATHEMATICS .....PRESS M"
3650 PRINT "TO EDIT MARKS IN PHYSICAL SCIENCES .....PRESS P"
3660 PRINT "TO EDIT MARKS IN LIFE SCIENCE .....PRESS L"
3670 PRINT "TO EDIT MARKS IN HISTORY .....PRESS H"
3680 PRINT "TO EDIT MARKS IN GEOGRAPHY .....PRESS G"
3690 PRINT "TO APPLY THE CHANGES .....PRESS A"
3700 PRINT "TO DISCARD THE CHANGES .....PRESS D"
3710 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 3710
3720 IF CHK$ = "F" OR CHK$ = "f" THEN GOSUB 3830: GOTO 3820
3730 IF CHK$ = "S" OR CHK$ = "s" THEN GOSUB 3860: GOTO 3820
3740 IF CHK$ = "M" OR CHK$ = "m" THEN GOSUB 3890: GOTO 3820
3750 IF CHK$ = "P" OR CHK$ = "p" THEN GOSUB 3920: GOTO 3820
3760 IF CHK$ = "L" OR CHK$ = "l" THEN GOSUB 3950: GOTO 3820
3770 IF CHK$ = "H" OR CHK$ = "h" THEN GOSUB 3980: GOTO 3820
3780 IF CHK$ = "G" OR CHK$ = "g" THEN GOSUB 4010: GOTO 3820
3790 IF CHK$ = "A" OR CHK$ = "a" THEN GOTO 3810
3800 IF CHK$ = "D" OR CHK$ = "d" THEN WRITE #2, AMID(PI), AMFL(PI), AMSL(PI), AMMAT(PI), AMPSC(PI), AMLSC(PI), AMHST(PI), AMGEO(PI): GOTO 4040 ELSE BEEP: GOTO 3710
3810 FLAGC=1: WRITE #2, DID, CFL, CSL, CMAT, CPSC, CLSC, CHST, CGEO: GOTO 4040
3820 GOTO 3510
3830 CLS: PRINT "MARKS IN FIRST LANGUAGE:";CFL
3840 INPUT "ENTER NEW MARKS IN FIRST LANGUAGE";CFL: IF CFL<0 OR CFL>200 THEN BEEP: PRINT "INVALID DATA": GOTO 3830
3850 RETURN
3860 CLS: PRINT "MARKS IN SECOND LANGUAGE:";CSL
3870 INPUT "ENTER NEW MARKS IN SECOND LANGUAGE";CSL: IF CSL<0 OR CSL>100 THEN BEEP: PRINT "INVALID DATA": GOTO 3860
3880 RETURN
3890 CLS: PRINT "MARKS IN MATHEMATICS:";CMAT
3900 INPUT "ENTER NEW MARKS IN MATHEMATICS";CMAT: IF CMAT<0 OR CMAT>100 THEN BEEP: PRINT "INVALID DATA": GOTO 3890
3910 RETURN
3920 CLS: PRINT "MARKS IN PHYSICAL SCIENCES:";CPSC
3930 INPUT "ENTER NEW MARKS IN PHYSICAL SCIENCES";CPSC: IF CPSC<0 OR CPSC>100 THEN BEEP: PRINT "INVALID DATA": GOTO 3920
3940 RETURN
3950 CLS: PRINT "MARKS IN LIFE SCIENCE:";CLSC
3960 INPUT "ENTER NEW MARKS IN LIFE SCIENCE";CLSC: IF CLSC<0 OR CLSC>100 THEN BEEP: PRINT "INVALID DATA": GOTO 3950
3970 RETURN
3980 CLS: PRINT "MARKS IN HISTORY:";CHST
3990 INPUT "ENTER NEW MARKS IN HISTORY";CHST: IF CHST<0 OR CHST>100 THEN BEEP: PRINT "INVALID DATA": GOTO 3980
4000 RETURN
4010 CLS: PRINT "MARKS IN GEOGRAPHY:";CGEO
4020 INPUT "ENTER NEW MARKS IN GEOGRAPHY";CGEO: IF CGEO<0 OR CGEO>200 THEN BEEP: PRINT "INVALID DATA": GOTO 4010
4030 RETURN
4040 IF FLAGF=0 THEN  BEEP: PRINT "MARKSHEET NOT FOUND": GOTO 4060
4050 IF FLAGC=1 THEN PRINT "--------------------------RECORD CHANGED SUCCESSFULLY---------------------------"
4060 CLOSE #2
4070 PRINT: PRINT "DO YOU WANT TO EDIT ANOTHER MARKSHEET? (Y/N)"
4080 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 4080
4090 IF CHK$ = "Y" OR CHK$ = "y" THEN GOTO 3340
4100 IF CHK$ = "N" OR CHK$ = "n" THEN GOTO 4110 ELSE BEEP: GOTO 4080
4110 RETURN


4120 'SUBROUTINE FOR DELETE MARKSHEET
4130 GOSUB 2940
4140 CLS: INPUT "ENTER ID";DID
4150 OPEN "STUDENT.TXT" FOR INPUT AS #3: FLAGF=0: FLAGD=0
4160 WHILE NOT EOF(3)
4170 INPUT #3, ID, F$, S$, CL, SEC$, RN
4180 IF DID=ID THEN FLAGF=1: DID=ID: DF$=F$: DS$=S$: DCL=CL: DSEC$=SEC$: DRN=RN
4190 WEND
4200 CLOSE #3
4210 IF FLAGF=0 THEN BEEP: PRINT "RECORD NOT FOUND": GOTO 4450
4220 OPEN "MARKS.TXT" FOR OUTPUT AS #1
4230 FOR I = 1 TO K
4240 IF AMID(I)<>DID THEN GOTO 4400
4250 FLAGD=1
4260 CLS: COLOR 9: PRINT "------------------------------STUDENT  INFORMATION-----------------------------": COLOR 15
4270 PRINT "ID:"; DID; "   NAME: "; DF$+" "+DS$; "    CLASS:"; DCL; "   SECTION: "; DSEC$; "    ROLL:"; DRN
4280 COLOR 9: PRINT "-----------------------------------MARKSHEET-----------------------------------": COLOR 15
4290 PRINT "FIRST LANGUAGE    :";AMFL(I)
4300 PRINT "SECOND LANGUAGE   :";AMSL(I)
4310 PRINT "MATHEMATICS       :";AMMAT(I)
4320 PRINT "PHYSICAL SCIENCES :";AMPSC(I)
4330 PRINT "LIFE SCIENCE      :";AMLSC(I)
4340 PRINT "HISTORY           :";AMHST(I)
4350 PRINT "GEOGRAPHY         :";AMGEO(I)
4360 PRINT: PRINT "ARE YOU SURE YOU WANT TO DELETE THIS MARKSHEET? (Y/N)"
4370 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 4370
4380 IF CHK$ = "Y" OR CHK$ = "y" THEN FLAGD=1: GOTO 4410
4390 IF CHK$ = "N" OR CHK$ = "n" THEN FLAGD=0: GOTO 4400 ELSE BEEP: GOTO 4370
4400 WRITE #1, AMID(I), AMFL(I), AMSL(I), AMMAT(I), AMPSC(I), AMLSC(I), AMHST(I), AMGEO(I)
4410 NEXT I
4420 IF FLAGF=0 THEN  BEEP: PRINT "MARKSHEET NOT FOUND": GOTO 4450
4430 IF FLAGD=1 THEN PRINT "------------------------MARKSHEET DELETED SUCCESSFULLY--------------------------"
4440 CLOSE #1
4450 PRINT: PRINT "DO YOU WANT TO DELETE ANOTHER MARKSHEET? (Y/N)"
4460 CHK$ = INKEY$: IF CHK$ = "" THEN GOTO 4460
4470 IF CHK$ = "Y" OR CHK$ = "y" THEN GOTO 4120
4480 IF CHK$ = "N" OR CHK$ = "n" THEN GOTO 4490 ELSE BEEP: GOTO 4460
4490 RETURN


4500 'SUBROUTINE FOR REPEAT MARKSHEET CHECK
4510 FLAGI=0: OPEN "MARKS.TXT" FOR INPUT AS #1
4520 WHILE NOT EOF(1)
4530 INPUT #1, ID, FL, SL, MAT, PSC, LSC, HST, GEO
4540 IF EID=ID THEN FLAGI=1
4550 WEND
4570 CLOSE #1
4580 IF FLAGI=1 THEN BEEP: PRINT "MARKSHEET OF THIS ID ALREADY EXISTS"
4590 RETURN


4600 'SUBROUTINE FOR REPEAT RECORD CHECK
4610 FLAGCID=0: OPEN "STUDENT.TXT" FOR INPUT AS #1
4620 WHILE NOT EOF(1)
4630 INPUT #1, ID, F$, S$, CL, SEC$, RN
4640 IF CID=ID THEN FLAGCID=1
4650 WEND
4660 CLOSE #1
4670 IF FLAGCID=1 THEN BEEP: PRINT "STUDENT INFORMATION OF THIS ID ALREADY EXISTS"
4680 RETURN


4700 'AUTHOR: ASMIT DE     FINISH DATE: NOVEMBER 26, 2007     TIME: 2:58AM